PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX rr: <http://www.w3.org/ns/r2rml#>

SELECT ?predicate ?domainClass ?rangeClass ?columnUrl
WHERE {
    # Find the predicate and the range URI pattern
    ?s rr:predicateObjectMap [
      rr:predicate ?predicate;
      rr:objectMap [
			rr:template ?uriRange;
        ];
    ].
    # Find the domain for the same mapping object (variable "s" in this case)
    ?s rr:subjectMap [
      rr:template ?uriDomain;
    ].
    
    # Remove query-specific column names from the URI patterns
    BIND (replace(?uriRange, "\\{.*\\}", '') AS ?templateRange).
    BIND (replace(?uriDomain, "\\{.*\\}", '') AS ?templateDomain).
    
    # Match generic URI pattern for range to the class definition
    ?domainMap rr:subjectMap [
      rr:template ?realDomainUrl;
      rr:class ?domainClass;
    ].
    FILTER(regex(?realDomainUrl, ?templateDomain)).
    
    # Match generic URI pattern for domain to the class definition
    ?rangeMap rr:subjectMap [
      rr:template ?realRangeUrl;
      rr:class ?rangeClass;
    ].
    FILTER(regex(?realRangeUrl, ?templateRange)).
    
    # Take the predicate and remove the foreign-key relationship to identify the actual column.
    BIND (replace(str(?predicate), "^.*ref-", '') AS ?columnUrl).
}
